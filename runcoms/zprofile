#
# Executes commands at login pre-zshrc.
#
# Authors:
#   Sorin Ionescu <sorin.ionescu@gmail.com>
#

#
# Browser
#

if [[ "$OSTYPE" == darwin* ]]; then
  export BROWSER='open'
fi

#
# Editors
#

export EDITOR='vim'
export VISUAL='vim'
export PAGER='less'

#
# Language
#

if [[ -z "$LANG" ]]; then
  export LC_CTYPE='en_CA.UTF-8'
  export LANG='en_US.UTF-8'
fi

#
# Paths
#

# Ensure path arrays do not contain duplicates.
typeset -gU cdpath fpath mailpath path manpath

# Set the list of directories that cd searches.
# cdpath=(
#   $cdpath
# )

# Set the list of directories that Zsh searches for programs.
path=(
  /usr/local/{bin,sbin}
  $path
)

# PREPEND pyenv's shims, if it exists!
if [ -d "$HOME/.pyenv/shims" ] ; then
  path=(
    "$HOME/.pyenv/shims"
    $path
  )
fi

# append ~/.local/bin (pip likes to put things here)
if [ -d "$HOME/.local/bin" ] ; then
  path+=("$HOME/.local/bin")
fi

##
# Erlang.
##
if [ -d /usr/local/opt/erlang/lib/erlang/man ]; then
  manpath+=(
    /usr/local/opt/erlang/lib/erlang/man
  )
fi

#
# Less
#

# Set the default Less options.
# Mouse-wheel scrolling has been disabled by -X (disable screen clearing).
# Remove -X and -F (exit if the content fits on one screen) to enable it.
lessopt=(
  --hilite-search
  --ignore-case
  --long-prompt
  --RAW-CONTROL-CHARS # Displays ANSI color escapes, but nothing else.
  --chop-long-lines
)
export LESS="${lessopt}"

# Set the Less input preprocessor.
# Try both `lesspipe` and `lesspipe.sh` as either might exist on a system.
if (( $#commands[(i)lesspipe(|.sh)] )); then
  export LESSOPEN="| /usr/bin/env $commands[(i)lesspipe(|.sh)] %s 2>&-"
fi

##
# lessfilter
# TODO: try https://github.com/wofr06/lesspipe
if (($+commands[lessfilter])); then
  export LESSOPEN="|$commands[lessfilter] %s"
fi

#
# Temporary Files
#

if [[ ! -d "$TMPDIR" ]]; then
  export TMPDIR="/tmp/$LOGNAME"
  mkdir -p -m 700 "$TMPDIR"
fi

TMPPREFIX="${TMPDIR%/}/zsh"
